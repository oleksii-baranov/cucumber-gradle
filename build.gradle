apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'

ext.testRepoURL = file("$buildDir/repo").toURI().toURL()

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
}

task testPlugin(type: GradleBuild) {
    buildFile file('src/test/example-project/build.gradle')
    dir file('src/test/example-project')
    startParameter.projectProperties['mavenRepo'] = testRepoURL
    startParameter.projectProperties['cukeTags'] = '~@wip'
    tasks += 'cucumber'
}

task installTestPlugin(type: Upload) {
    repositories {
        maven {
            url testRepoURL
        }
    }
    configuration = configurations.archives
}

uploadArchives {
    uploadDescriptor = true
    repositories {
        mavenDeployer {
            repository(url: mavenDeployUrl) {
                authentication(userName: mavenDeployUsername, password: mavenDeployPassword)
            }
        }
    }
}

testPlugin.dependsOn installTestPlugin
test.dependsOn testPlugin
